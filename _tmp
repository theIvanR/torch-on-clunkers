@echo off
setlocal EnableExtensions EnableDelayedExpansion

REM ===============================
REM Kill Intel & force MSVC
REM ===============================
set CC=cl
set CXX=cl
set CMAKE_C_COMPILER=cl
set CMAKE_CXX_COMPILER=cl

set ONEAPI_ROOT=
set ICPP_COMPILER=
set ICX=
set ICC=


REM ===============================
REM A: Set MKL Paths
REM ===============================
set "MKLROOT=C:\Program Files (x86)\Intel\oneAPI\mkl\latest"
set "CMAKE_PREFIX_PATH=%MKLROOT%"
set "LIB=%LIB%;%MKLROOT%\lib\intel64"
set "INCLUDE=%INCLUDE%;%MKLROOT%\include"
set "PATH=%PATH%;%MKLROOT%\bin"


REM ===============================
REM B: Set Cuda Paths
REM ===============================
set "CUDA_ROOT=C:/Program Files/NVIDIA GPU Computing Toolkit/CUDA/v11.4"
set "CUDNN_ROOT=C:/Program Files/NVIDIA GPU Computing Toolkit/CUDNN/cudnn-windows-x86_64-8.7.0.84_cuda11-archive"


REM ===============================
REM C: Set Torch Paths and Investigate in them
REM ===============================
set "SRC_DIR=C:/Users/%USERNAME%/source/pytorch"
set "BUILD_DIR=build"


pushd "%SRC_DIR%" || (
    echo [ERROR] Unable to enter %SRC_DIR%
    exit /b 1
)

REM Optional clean build artifacts
REM if exist "%BUILD_DIR%" rmdir /s /q "%BUILD_DIR%"




REM ===============================
REM Configure
REM ===============================
cmake -S . -B "%BUILD_DIR%" ^
    -G Ninja ^
    -DCMAKE_BUILD_TYPE=Release ^
    -DCUDA_TOOLKIT_ROOT_DIR="%CUDA_ROOT%" ^
    -DCUDNN_ROOT="%CUDNN_ROOT%" ^
    -DTORCH_CUDA_ARCH_LIST="3.5;3.7;5.0;5.2;6.0;6.1;7.0;7.5" ^
    -DUSE_CUDA=ON ^
    -DUSE_CUDNN=ON ^
    -DUSE_MKL=ON ^
    -DBLAS=MKL ^
    -DUSE_XNNPACK=OFF ^
    -DUSE_DISTRIBUTED=OFF ^
    -DUSE_TENSORPIPE=OFF

REM ===============================
REM Build CUDA first, then remaining files
REM ===============================
cmake --build "%BUILD_DIR%" --target torch_cuda -- -j %NUMBER_OF_PROCESSORS%
cmake --build "%BUILD_DIR%" -- -j %NUMBER_OF_PROCESSORS%

REM ===============================
REM Python wheel and celebrate
REM ===============================
set DISTUTILS_USE_SDK=1
python setup.py bdist_wheel

popd
echo.
echo ===== BUILD COMPLETE =====
exit /b 0
